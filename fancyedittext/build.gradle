apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

def siteUrl = 'https://github.com/Rairmmd/FancyEditText'
def gitUrl = 'https://github.com/Rairmmd/FancyEditText.git'

group = "com.rairmmd"
version = "1.0.0"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        consumerProguardFiles "consumer-rules.pro"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'FancyEditText'
                description 'FancyEditText for Android'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'rairmmd'
                        name 'rairmmd'
                        email 'rairmmd@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//配置源码
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
//配置文档
task androidJavadoc(type: Javadoc) {
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/R.html', '**/R.*.html', '**/index.html', '**/*.kt'
}
task androidJavadocJar(type: Jar, dependsOn: androidJavadoc) {
    classifier = 'javadoc'
    from androidJavadoc.destinationDir
}
artifacts {
    archives androidJavadocJar
    archives androidSourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    override = true
    publish = true
    configurations = ['archives']
    pkg {
        //仓库
        repo = "maven"
        //仓库名
        name = "FancyEditText"

        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}